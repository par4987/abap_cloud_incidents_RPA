managed implementation in class zbp_r_incidents_1237 unique;
strict ( 2 );
with draft;

define behavior for ZR_DT_INCT_1237 alias Incidents
persistent table zdt_inct_1237
draft table zdt_inct_1237_d
etag master LocalLastChangedAt
lock master total etag LastChangedAt
authorization master ( global, instance )

{
create ( authorization: global );
update;
delete;

  association _History { create ( features : instance ); with draft; } // Association to the History CDS


  field ( readonly )
  IncUUID,
  IncidentID,
  Status,
  ChangedDate,
  LocalCreatedBy,
  LocalCreatedAt,
  LocalLastChangedBy,
  LocalLastChangedAt,
  LastChangedAt;

  field ( numbering : managed )      // Managed numbering for IncidentID
  IncUUID;

  field (mandatory)                  // Mandatory fields for Incident according to requirements
   title,
   Description,
   Priority;

action ( features : instance, authorization : update ) changeStatus
    parameter zc_ae_change_status_1237  result [1] $self;  // Parameters from the abstract CDS * Result self update and gives back the instance

  side effects { action changeStatus affects $self; }

  internal action setHistory;

  determination setDefaultHistory on save { create; }
  determination setDefaultValues on modify { create; }

  draft action Activate optimized;
  draft action Discard;
  draft action Edit;
  draft action Resume;
  draft determine action Prepare;

 mapping for zdt_inct_1237
    {
      IncUUID            = inc_uuid;
      IncidentID         = incident_id;
      Title              = title;
      Description        = description;
      Status             = status;
      Priority           = priority;
      CreationDate       = creation_date;
      ChangedDate        = changed_date;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }

}


// Behavior for Incidents History

define behavior for ZR_DT_INCT_H_1237 alias History
persistent table zdt_inct_h_1237
draft table zdt_inct_h_1237d
etag master LocalLastChangedAt
lock dependent by _Incident
authorization dependent by _Incident  // Authorization dependent on the Incidents parent association
{
  update;
  delete;

  association _Incident { with draft; }

  field ( numbering : managed )
  HisUUID;

  field ( readonly )
  HisUUID,
  IncUUID,
  LocalCreatedBy,
  LocalCreatedAt,
  LocalLastChangedBy,
  LocalLastChangedAt,
  LastChangedAt;

  mapping for zdt_inct_h_1237
    {
      HisUUID            = his_uuid;
      IncUUID            = inc_uuid;
      HisID              = his_id;
      PreviousStatus     = previous_status;
      NewStatus          = new_status;
      Text               = text;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }
}